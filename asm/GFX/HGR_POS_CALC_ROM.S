NEW

10 HGR : REM HGR2
20 HEX$ = "0123456789ABCDEF" : J = 0
30 FOR I = 0 TO 191
40 POKE 226,I : POKE 224,J : CALL 24576 : REM FETCH HGR ADDRESS
50 POKE PEEK(39)*256+PEEK(38),255     : REM WRITE AT HGR ADDRESS
60 CALL 24576+36                      : REM PRINT ADDRESS + SPC
90 NEXT I: END

CALL-151

6000: A5 E2 48 29 C0 85 26 4A
6008: 4A 05 26 85 26 68 85 27
6010: 0A 0A 0A 26 27 0A 26 27
6018: 0A 66 26 A5 27 29 1F 05
6020: E6 85 27 60 A6 26 A4 27
6028: 20 40 F9 A9 A0 20 F0 FD
6030: 60 

E003G
RUN



------------

; CALCULATE HGR-HGR2 BASE POSITIONS
*=$6000

GBASL       = $26
GBASH       = $27
HGR.Y       = $E2
HGR.X       = $E0
HGR.PAGE    = $E6

START   LDA *HGR.Y      ; Y ORDINATE

;---- CALCULATE LOW BYTE FOR GFX POS ---

        PHA
        AND #$C0        ; CALCULATE BASE ADDRESS FOR Y-POS
        STA *GBASL      ; FOR Y=ABCDEFGH
        LSR A           ; GBASL=ABAB0000
        LSR A
        ORA *GBASL
        STA *GBASL

        PLA            ;   (A)      (GBASH)   (GBASL)
        STA *GBASH     ;?-ABCDEFGH  ABCDEFGH  ABAB0000
        ASL A          ;A-BCDEFGH0  ABCDEFGH  ABAB0000
        ASL A          ;B-CDEFGH00  ABCDEFGH  ABAB0000
        ASL A          ;C-DEFGH000  ABCDEFGH  ABAB0000
        ROL *GBASH     ;A-DEFGH000  BCDEFGHC  ABAB0000
        ASL A          ;D-EFGH0000  BCDEFGHC  ABAB0000
        ROL *GBASH     ;B-EFGH0000  CDEFGHCD  ABAB0000
        ASL A          ;E-FGH00000  CDEFGHCD  ABAB0000
        ROR *GBASL     ;0-FGH00000  CDEFGHCD  EABAB000
        LDA *GBASH     ;0-CDEFGHCD  EABAB000
        AND #$1F       ;0-000FGHCD  CDEFGHCD  EABAB000
        ORA *HGR.PAGE  ;0-PPPFGHCD  CDEFGHCD  EABAB000
        STA *GBASH     ;0-PPPFGHCD  PPPFGHCD  EABAB000
        RTS

PRTADR  LDX *GBASL
        LDY *GBASH
        JSR $F940      ; PRINT RESULT ADDRESS
        LDA #$A0
        JSR $FDF0      ; PRINT SPC
        ;JSR $FD67     ; PRINT LF
        RTS
